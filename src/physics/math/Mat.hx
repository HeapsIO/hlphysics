package physics.math;

@:struct
class MatImpl {
	public var _11 : Scalar;
	public var _12 : Scalar;
	public var _13 : Scalar;
	public var _14 : Scalar;
	public var _21 : Scalar;
	public var _22 : Scalar;
	public var _23 : Scalar;
	public var _24 : Scalar;
	public var _31 : Scalar;
	public var _32 : Scalar;
	public var _33 : Scalar;
	public var _34 : Scalar;
	public var _41 : Scalar;
	public var _42 : Scalar;
	public var _43 : Scalar;
	public var _44 : Scalar;

	public inline function new() {
	}

	public function equals( other : Mat ) {
		return _11 == other._11 && _12 == other._12 && _13 == other._13 && _14 == other._14
			&& _21 == other._21 && _22 == other._22 && _23 == other._23 && _24 == other._24
			&& _31 == other._31 && _32 == other._32 && _33 == other._33 && _34 == other._34
			&& _41 == other._41 && _42 == other._42 && _43 == other._43 && _44 == other._44;
	}

	public inline function initIdentity() {
		_11 = 1.0; _12 = 0.0; _13 = 0.0; _14 = 0.0;
		_21 = 0.0; _22 = 1.0; _23 = 0.0; _24 = 0.0;
		_31 = 0.0; _32 = 0.0; _33 = 1.0; _34 = 0.0;
		_41 = 0.0; _42 = 0.0; _43 = 0.0; _44 = 1.0;
	}

	public inline function initTranslation( x : Scalar = 0., y : Scalar = 0., z : Scalar = 0. ) {
		_11 = 1.0; _12 = 0.0; _13 = 0.0; _14 = 0.0;
		_21 = 0.0; _22 = 1.0; _23 = 0.0; _24 = 0.0;
		_31 = 0.0; _32 = 0.0; _33 = 1.0; _34 = 0.0;
		_41 = x; _42 = y; _43 = z; _44 = 1.0;
	}

	public inline function initScale( x : Scalar = 1., y : Scalar = 1., z : Scalar = 1. ) {
		_11 = x; _12 = 0.0; _13 = 0.0; _14 = 0.0;
		_21 = 0.0; _22 = y; _23 = 0.0; _24 = 0.0;
		_31 = 0.0; _32 = 0.0; _33 = z; _34 = 0.0;
		_41 = 0.0; _42 = 0.0; _43 = 0.0; _44 = 1.0;
	}

	public inline function initRotationQuat( q : Quat ) {
		var x = q.x;
		var y = q.y;
		var z = q.z;
		var w = q.w;

		var tx = x + x;
		var ty = y + y;
		var tz = z + z;

		var xx = tx * x;
		var yy = ty * y;
		var zz = tz * z;
		var xy = tx * y;
		var xz = tx * z;
		var xw = tx * w;
		var yz = ty * z;
		var yw = ty * w;
		var zw = tz * w;

		_11 = (1.0 - yy) - zz;
		_12 = xy + zw;
		_13 = xz - yw;
		_14 = 0.0;
		_21 = xy - zw;
		_22 = (1.0 - zz) - xx;
		_23 = yz + xw;
		_24 = 0.0;
		_31 = xz + yw;
		_32 = yz - xw;
		_33 = (1.0 - xx) - yy;
		_34 = 0.0; _41 = 0.0; _42 = 0.0; _43 = 0.0; _44 = 1.0;
	}

	public inline function initRotationAxis( axis : Vec3, angle : Scalar ) {
		var cos = Math.cos(angle), sin = Math.sin(angle);
		var cos1 = 1.0 - cos;
		var x = -axis.x, y = -axis.y, z = -axis.z;
		var xx = x * x, yy = y * y, zz = z * z;
		var len = Math.invSqrt(xx + yy + zz);
		x *= len;
		y *= len;
		z *= len;
		var xcos1 = x * cos1, zcos1 = z * cos1;
		_11 = cos + x * xcos1;
		_12 = y * xcos1 - z * sin;
		_13 = x * zcos1 + y * sin;
		_14 = 0.;
		_21 = y * xcos1 + z * sin;
		_22 = cos + y * y * cos1;
		_23 = y * zcos1 - x * sin;
		_24 = 0.;
		_31 = x * zcos1 - y * sin;
		_32 = y * zcos1 + x * sin;
		_33 = cos + z * zcos1;
		_34 = 0.;
		_41 = 0.; _42 = 0.; _43 = 0.; _44 = 1.;
	}

	public inline function initRotation( x : Scalar, y : Scalar, z : Scalar ) {
		var cx = Math.cos(x);
		var sx = Math.sin(x);
		var cy = Math.cos(y);
		var sy = Math.sin(y);
		var cz = Math.cos(z);
		var sz = Math.sin(z);
		var cxsy = cx * sy;
		var sxsy = sx * sy;
		_11 = cy * cz;
		_12 = cy * sz;
		_13 = -sy;
		_14 = 0.0;
		_21 = sxsy * cz - cx * sz;
		_22 = sxsy * sz + cx * cz;
		_23 = sx * cy;
		_24 = 0.0;
		_31 = cxsy * cz + sx * sz;
		_32 = cxsy * sz - sx * cz;
		_33 = cx * cy;
		_34 = 0.0;
		_41 = 0.0;
		_42 = 0.0;
		_43 = 0.0;
		_44 = 1.0;
	}

	public inline function translate( x : Scalar = 0., y : Scalar = 0., z : Scalar = 0. ) {
		_11 += x * _14;
		_12 += y * _14;
		_13 += z * _14;
		_21 += x * _24;
		_22 += y * _24;
		_23 += z * _24;
		_31 += x * _34;
		_32 += y * _34;
		_33 += z * _34;
		_41 += x * _44;
		_42 += y * _44;
		_43 += z * _44;
	}

	public inline function scale( x : Scalar = 1., y : Scalar = 1., z : Scalar = 1. ) {
		_11 *= x;
		_21 *= x;
		_31 *= x;
		_41 *= x;
		_12 *= y;
		_22 *= y;
		_32 *= y;
		_42 *= y;
		_13 *= z;
		_23 *= z;
		_33 *= z;
		_43 *= z;
	}

	public inline function scaled( x : Scalar, y : Scalar, z : Scalar ) {
		var tmp = clone();
		tmp.scale(x, y, z);
		return tmp;
	}

	public inline function rotate( x : Scalar, y : Scalar, z : Scalar) {
		var tmp = new Mat();
		tmp.initRotation(x,y,z);
		multiply3x4inline(this, tmp);
	}

	public inline function rotateAxis( axis : Vec3, angle : Scalar ) {
		var tmp = new Mat();
		tmp.initRotationAxis(axis, angle);
		multiply3x4inline(this, tmp);
	}

	public inline function transpose() {
		var tmp : Scalar;
		tmp = _12; _12 = _21; _21 = tmp;
		tmp = _13; _13 = _31; _31 = tmp;
		tmp = _14; _14 = _41; _41 = tmp;
		tmp = _23; _23 = _32; _32 = tmp;
		tmp = _24; _24 = _42; _42 = tmp;
		tmp = _34; _34 = _43; _43 = tmp;
	}

	public inline function transposed() {
		var tmp = clone();
		tmp.transpose();
		return tmp;
	}

	public inline function clone() {
		var m = new Mat();
		m._11 = _11; m._12 = _12; m._13 = _13; m._14 = _14;
		m._21 = _21; m._22 = _22; m._23 = _23; m._24 = _24;
		m._31 = _31; m._32 = _32; m._33 = _33; m._34 = _34;
		m._41 = _41; m._42 = _42; m._43 = _43; m._44 = _44;
		return m;
	}

	public inline function load( m : Mat ) {
		_11 = m._11; _12 = m._12; _13 = m._13; _14 = m._14;
		_21 = m._21; _22 = m._22; _23 = m._23; _24 = m._24;
		_31 = m._31; _32 = m._32; _33 = m._33; _34 = m._34;
		_41 = m._41; _42 = m._42; _43 = m._43; _44 = m._44;
	}

	public inline function multiply3x4inline( a : Mat, b : Mat ) {
		var m11 = a._11; var m12 = a._12; var m13 = a._13;
		var m21 = a._21; var m22 = a._22; var m23 = a._23;
		var a31 = a._31; var a32 = a._32; var a33 = a._33;
		var a41 = a._41; var a42 = a._42; var a43 = a._43;
		var b11 = b._11; var b12 = b._12; var b13 = b._13;
		var b21 = b._21; var b22 = b._22; var b23 = b._23;
		var b31 = b._31; var b32 = b._32; var b33 = b._33;
		var b41 = b._41; var b42 = b._42; var b43 = b._43;

		_11 = m11 * b11 + m12 * b21 + m13 * b31;
		_12 = m11 * b12 + m12 * b22 + m13 * b32;
		_13 = m11 * b13 + m12 * b23 + m13 * b33;
		_14 = 0.0;

		_21 = m21 * b11 + m22 * b21 + m23 * b31;
		_22 = m21 * b12 + m22 * b22 + m23 * b32;
		_23 = m21 * b13 + m22 * b23 + m23 * b33;
		_24 = 0.0;

		_31 = a31 * b11 + a32 * b21 + a33 * b31;
		_32 = a31 * b12 + a32 * b22 + a33 * b32;
		_33 = a31 * b13 + a32 * b23 + a33 * b33;
		_34 = 0.0;

		_41 = a41 * b11 + a42 * b21 + a43 * b31 + b41;
		_42 = a41 * b12 + a42 * b22 + a43 * b32 + b42;
		_43 = a41 * b13 + a42 * b23 + a43 * b33 + b43;
		_44 = 1.0;
	}

	public inline function multiply( a : Mat, b : Mat ) {
		var a11 = a._11; var a12 = a._12; var a13 = a._13; var a14 = a._14;
		var a21 = a._21; var a22 = a._22; var a23 = a._23; var a24 = a._24;
		var a31 = a._31; var a32 = a._32; var a33 = a._33; var a34 = a._34;
		var a41 = a._41; var a42 = a._42; var a43 = a._43; var a44 = a._44;
		var b11 = b._11; var b12 = b._12; var b13 = b._13; var b14 = b._14;
		var b21 = b._21; var b22 = b._22; var b23 = b._23; var b24 = b._24;
		var b31 = b._31; var b32 = b._32; var b33 = b._33; var b34 = b._34;
		var b41 = b._41; var b42 = b._42; var b43 = b._43; var b44 = b._44;

		_11 = a11 * b11 + a12 * b21 + a13 * b31 + a14 * b41;
		_12 = a11 * b12 + a12 * b22 + a13 * b32 + a14 * b42;
		_13 = a11 * b13 + a12 * b23 + a13 * b33 + a14 * b43;
		_14 = a11 * b14 + a12 * b24 + a13 * b34 + a14 * b44;

		_21 = a21 * b11 + a22 * b21 + a23 * b31 + a24 * b41;
		_22 = a21 * b12 + a22 * b22 + a23 * b32 + a24 * b42;
		_23 = a21 * b13 + a22 * b23 + a23 * b33 + a24 * b43;
		_24 = a21 * b14 + a22 * b24 + a23 * b34 + a24 * b44;

		_31 = a31 * b11 + a32 * b21 + a33 * b31 + a34 * b41;
		_32 = a31 * b12 + a32 * b22 + a33 * b32 + a34 * b42;
		_33 = a31 * b13 + a32 * b23 + a33 * b33 + a34 * b43;
		_34 = a31 * b14 + a32 * b24 + a33 * b34 + a34 * b44;

		_41 = a41 * b11 + a42 * b21 + a43 * b31 + a44 * b41;
		_42 = a41 * b12 + a42 * b22 + a43 * b32 + a44 * b42;
		_43 = a41 * b13 + a42 * b23 + a43 * b33 + a44 * b43;
		_44 = a41 * b14 + a42 * b24 + a43 * b34 + a44 * b44;
	}

	public inline function getInverse( ) {
		var mout = new Mat();
		mout.initInverse(this);
		return mout;
	}

	public inline function initInverse( m : Mat ) {
		var m11 = m._11; var m12 = m._12; var m13 = m._13; var m14 = m._14;
		var m21 = m._21; var m22 = m._22; var m23 = m._23; var m24 = m._24;
		var m31 = m._31; var m32 = m._32; var m33 = m._33; var m34 = m._34;
		var m41 = m._41; var m42 = m._42; var m43 = m._43; var m44 = m._44;

		_11 = m22 * m33 * m44 - m22 * m34 * m43 - m32 * m23 * m44 + m32 * m24 * m43 + m42 * m23 * m34 - m42 * m24 * m33;
		_12 = -m12 * m33 * m44 + m12 * m34 * m43 + m32 * m13 * m44 - m32 * m14 * m43 - m42 * m13 * m34 + m42 * m14 * m33;
		_13 = m12 * m23 * m44 - m12 * m24 * m43 - m22 * m13 * m44 + m22 * m14 * m43 + m42 * m13 * m24 - m42 * m14 * m23;
		_14 = -m12 * m23 * m34 + m12 * m24 * m33 + m22 * m13 * m34 - m22 * m14 * m33 - m32 * m13 * m24 + m32 * m14 * m23;
		_21 = -m21 * m33 * m44 + m21 * m34 * m43 + m31 * m23 * m44 - m31 * m24 * m43 - m41 * m23 * m34 + m41 * m24 * m33;
		_22 = m11 * m33 * m44 - m11 * m34 * m43 - m31 * m13 * m44 + m31 * m14 * m43 + m41 * m13 * m34 - m41 * m14 * m33;
		_23 = -m11 * m23 * m44 + m11 * m24 * m43 + m21 * m13 * m44 - m21 * m14 * m43 - m41 * m13 * m24 + m41 * m14 * m23;
		_24 = m11 * m23 * m34 - m11 * m24 * m33 - m21 * m13 * m34 + m21 * m14 * m33 + m31 * m13 * m24 - m31 * m14 * m23;
		_31 = m21 * m32 * m44 - m21 * m34 * m42 - m31 * m22 * m44 + m31 * m24 * m42 + m41 * m22 * m34 - m41 * m24 * m32;
		_32 = -m11 * m32 * m44 + m11 * m34 * m42 + m31 * m12 * m44 - m31 * m14 * m42 - m41 * m12 * m34 + m41 * m14 * m32;
		_33 = m11 * m22 * m44 - m11 * m24 * m42 - m21 * m12 * m44 + m21 * m14 * m42 + m41 * m12 * m24 - m41 * m14 * m22;
		_34 = -m11 * m22 * m34 + m11 * m24 * m32 + m21 * m12 * m34 - m21 * m14 * m32 - m31 * m12 * m24 + m31 * m14 * m22;
		_41 = -m21 * m32 * m43 + m21 * m33 * m42 + m31 * m22 * m43 - m31 * m23 * m42 - m41 * m22 * m33 + m41 * m23 * m32;
		_42 = m11 * m32 * m43 - m11 * m33 * m42 - m31 * m12 * m43 + m31 * m13 * m42 + m41 * m12 * m33 - m41 * m13 * m32;
		_43 = -m11 * m22 * m43 + m11 * m23 * m42 + m21 * m12 * m43 - m21 * m13 * m42 - m41 * m12 * m23 + m41 * m13 * m22;
		_44 = m11 * m22 * m33 - m11 * m23 * m32 - m21 * m12 * m33 + m21 * m13 * m32 + m31 * m12 * m23 - m31 * m13 * m22;

		var det = m11 * _11 + m12 * _21 + m13 * _31 + m14 * _41;
		det = 1.0 / det;
		_11 *= det;
		_12 *= det;
		_13 *= det;
		_14 *= det;
		_21 *= det;
		_22 *= det;
		_23 *= det;
		_24 *= det;
		_31 *= det;
		_32 *= det;
		_33 *= det;
		_34 *= det;
		_41 *= det;
		_42 *= det;
		_43 *= det;
		_44 *= det;
	}

	public inline function getOrientation() {
		var m = new Mat();
		m._11 = _11; m._12 = _12; m._13 = _13; m._14 = _14;
		m._21 = _21; m._22 = _22; m._23 = _23; m._24 = _24;
		m._31 = _31; m._32 = _32; m._33 = _33; m._34 = _34;
		m._41 = 0.0; m._42 = 0.0; m._43 = 0.0; m._44 = 1.0;
		return m;
	}

	public inline function getPosition() {
		var v = new Vec3();
		v.set(_41,_42,_43);
		return v;
	}

	public inline function setPosition( x : Scalar, y : Scalar, z : Scalar ) {
		_41 = x;
		_42 = y;
		_43 = z;
	}
}

@:forward abstract Mat(MatImpl) from MatImpl to MatImpl {

	public inline function new() {
		this = new MatImpl();
	}

	@:op(a * b) public inline function multiplied( m : Mat ) {
		var mout = new Mat();
		mout.multiply(this, m);
		return mout;
	}

	public static inline function identity() {
		var m = new Mat();
		m.initIdentity();
		return m;
	}

	#if heaps
	public static inline function fromHeaps( m : h3d.Matrix ) {
		var mat = new Mat();
		mat._11 = m._11; mat._12 = m._12; mat._13 = m._13; mat._14 = m._14;
		mat._21 = m._21; mat._22 = m._22; mat._23 = m._23; mat._24 = m._24;
		mat._31 = m._31; mat._32 = m._32; mat._33 = m._33; mat._34 = m._34;
		mat._41 = m._41; mat._42 = m._42; mat._43 = m._43; mat._44 = m._44;
		return mat;
	}

	public inline function toHeaps() : h3d.Matrix {
		var m = new h3d.Matrix();
		m._11 = this._11; m._12 = this._12; m._13 = this._13; m._14 = this._14;
		m._21 = this._21; m._22 = this._22; m._23 = this._23; m._24 = this._24;
		m._31 = this._31; m._32 = this._32; m._33 = this._33; m._34 = this._34;
		m._41 = this._41; m._42 = this._42; m._43 = this._43; m._44 = this._44;
		return m;
	}
	#end
}
